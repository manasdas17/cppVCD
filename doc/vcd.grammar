value_change_dump_definitions ::=
	{ declaration_command }{ simulation_command }
declaration_command ::=
	declaration_keyword
	[ command_text ]
	$end
simulation_command ::=
	simulation_keyword { value_change } $end
	| $comment [ comment_text ] $end
	| simulation_time
	| value_change
declaration_keyword ::=
	$comment | $date | $enddefinitions | $scope | $timescale | $upscope
	| $var | $version
simulation_keyword ::=
	$dumpall | $dumpoff | $dumpon | $dumpvars
simulation_time ::=
	# decimal_number
value_change ::=
	scalar_value_change
	| vector_value_change
scalar_value_change ::=
	value identifier_code
value ::=
	0 | 1 | x | X | z | Z
vector_value_change ::=
	b binary_number identifier_code
	| B binary_number identifier_code
	| r real_number identifier_code
	| R real_number identifier_code
identifier_code ::=
	{ ASCII character }
